<command> = put-phrase | get-phrase | toggle-phrase | action | say
put-phrase = <"put"> <ws> object <(ws "in")?> <ws> target
get-phrase = <"get"> <ws> object (<ws "from" ws> target)?
toggle-phrase = flag <ws> state
say = <"say"> <ws> #".*"
state = "on" | "off"
flag = "echo"
object = token
target = token
<token> = #"[a-zA-Z]+"
ws = #"\s+"
<action> = direction
direction = north | south | east | west | northeast | northwest | southeast | southwest | 'in' | 'out'
north = <'n'> | <'north'>
south = <'s'> | <'south'>
east = <'e'> | <'east'>
west = <'w'> | <'west'>
northeast = <'ne'> | <'northeast'>
northwest = <'nw'> | <'northwest'>
southeast = <'se'> | <'southeast'>
southwest = <'sw'> | <'southwest'>
